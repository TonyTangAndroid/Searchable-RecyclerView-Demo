buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.github.triplet.play'
apply plugin: 'me.tatarka.retrolambda'

final travisVersionCode = hasProperty('version_code') ? property('version_code').toInteger() : 1
final travisVersionName = hasProperty('version_name') ? property('version_name') : '0.1.0.0'

final travisKeystorePassword = hasProperty('keystore_password') ? property('keystore_password') : ''
final travisKeyAlias = hasProperty('key_alias') ? property('key_alias') : ''
final travisKeyPassword = hasProperty('key_password') ? property('key_password') : ''

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId 'com.github.wrdlbrnft.searchablerecyclerviewdemo'
        minSdkVersion 14
        targetSdkVersion 25
        versionCode travisVersionCode
        versionName travisVersionName
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    lintOptions {
        disable 'NewApi'
    }

    signingConfigs {
        release {
            storeFile rootProject.file('release.jks')
            storePassword travisKeystorePassword
            keyAlias travisKeyAlias
            keyPassword travisKeyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.jakewharton:butterknife:8.6.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
// Because RxAndroid releases are few and far between, it is recommended you also
// explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'
}

play {
    track = 'beta'
    jsonFile = rootProject.file('keys.json')
}
